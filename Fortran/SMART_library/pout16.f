      SUBROUTINE POUT16(IA,I1,J1,ILL)
CS    CALL POUT16(IA,I1,J1,ILL)
CP****2 JIGEN NYUURYOKU HAIRETU NO NAIYOU WO PRINT SYUTURYOKU SURU
CP****POUT16 HA 16 TIKA ZUKEI WO INSATSU SURU
CP****POUT2B TO NO TIGAI HA NYUUTRYOKU DATA NO LEVEL SUU
CP****NOUDO LEVEL/0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15/ NI TAIOU SITE
CP****SOREZORE   / ,1,2,3,4,5,6,7,8,9, A, B, C, D, E, F/ WO INSATSU SURU
CA I  IA --- NYUURYOKU GAZOU (2 JIGEN HAIRETU)              I
CA I  I1 --- IA NO SEIGOU SUNPOU (DAI 1 SOEJI NO SUNPOU)    I
CA I  J1 --- IA NO SEIGOU SUNPOU (DAI 2 SOEJI NO SUNPOU)    I
CA O  ILL -- ILLEGAL PARAMETER                              I
CE    ILL=    0 --- SEIJYOU SYUURYOU
CE    ILL= 2000 --- NYUURYOKU DATA NO NAKA NI '0' KARA '15' MADE NO
CE                  HAN'I WO KOETA DATA GA HUKUMARETEIRU
CE                  SONO TEN HA 'X' TO INSATSU SURU
CE    ILL=30000 --- HIKISUU I1 MATAHA J1 GA HUTEKITOU
CE                  I1>130 MATAHA J1>200
CE                  KONO TOKI NIHA INSATSU SINAI
CD    1984.6.10    CODED BY TAKESHITA.TETSUO
***
      INTEGER IA(I1,J1)
***
CV    MOJI(132)*1 --- INSATSU YOU 1 GYOU BUN MOJI HAIRETSU
***
      CHARACTER MOJI(132)*1,TEMP*16
      DATA TEMP/' 123456789ABCDEF'/
***
CF    I1,J1 NO CHECK (IKA NO JYOUKEN WO MITASU KOTO)
CF                  I1<=130
CF                  J1<=200
***
      IF (I1 .GT. 130) THEN
        ILL=30000
        RETURN
      END IF
      IF (J1 .GT. 200) THEN
        ILL=30000
        RETURN
      END IF
      ILL=0
***
CF    MOJI(132)*1 HAIRETSU NO SYOKI SETTEI
***
      DO 50 I=1,132
        MOJI(I)=' '
   50 CONTINUE
***
CF    DAI 1 GYOU ME NO WAKU NO INSATSU
***
      DO 30 I=1,I1+2
      MOJI(I)='-'
   30 CONTINUE
      WRITE(6,100) ( MOJI(I),I=1,I1+2)
***
CF    DAI 2 GYOU ME KARA DAI (J+1) GYOUME NO NO INSATSU
***
      DO 20 J=1,J1
      MOJI(1)='|'
      DO 10 I=1,I1
        IF( (IA(I,J).LT.0).OR.(IA(I,J).GT.15)  )THEN
        MOJI(I+1)='X'
        ILL=2000
        ELSE
        KEYMO=IA(I,J)+1
        MOJI(I+1)=TEMP(KEYMO:KEYMO)
        ENDIF
   10 CONTINUE
      MOJI(I1+2)='|'
      WRITE(6,100) ( MOJI(I),I=1,I1+2)
   20 CONTINUE
***
CF    DAI (J+2) GYOU ME NO WAKU NO INSATSU
***
      DO 40 I=1,I1+2
      MOJI(I)='-'
   40 CONTINUE
      WRITE(6,100) ( MOJI(I),I=1,I1+2)
  100 FORMAT(1H ,132A1)
      RETURN
      END
