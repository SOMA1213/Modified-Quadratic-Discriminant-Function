      SUBROUTINE POUT2B(IA,I1,J1,ILL)
CS    CALL POUT2B(IA,I1,J1,ILL)
CP****2 JIGEN NYUURYOKU HAIRETU NO NAIYOU WO PRINT SYUTURYOKU SURU
CP****POUT2B HA 2 TIKA ZUKEI WO INSATSU SURU
CP****POUT16 TO NO TIGAI HA NYUUTRYOKU DATA NO LEVEL SUU
CA I  IA --- NYUURYOKU GAZOU (2 JIGEN HAIRETU)                    I
CA I  I1 --- IA NO SEIGOU SUNPOU (DAI 1 SOEJI NO SUNPOU) I1<=130  I
CA I  J1 --- IA NO SEIGOU SUNPOU (DAI 2 SOEJI NO SUNPOU) J1<=200  I
CA O  ILL -- ILLEGAL PARAMETER                                    I
CE    ILL=    0 --- SEIJYOU SYUURYOU
CE    ILL= 2000 --- NYUURYOKU DATA NO NAKA NI '0' TO '1' IGAI WO HUKUMU
CE                  SONO TEN HA 'X' TO INSATSU SURU
CE    ILL=30000 --- HIKISUU I1 MATAHA J1 GA HUTEKITOU
CE                  I1>130 MATAHA J1>200
CE                  KONO TOKI NIHA INSATSU SINAI
CD    1984.6.10    CODED BY TAKESHITA.TETSUO
***
      INTEGER IA(I1,J1)
***
CV    MOJI(132)*1 --- INSATSU YOU 1 GYOU BUN MOJI HAIRETSU
***
      CHARACTER MOJI(132)*1
***
CF    I1,J1 NO CHECK (IKA NO JYOUKEN WO MITASU KOTO)
CF                  I1<=130
CF                  J1<=200
***
      IF (I1 .GT. 130) THEN
        ILL=30000
        RETURN
      END IF
      IF (J1 .GT. 200) THEN
        ILL=30000
        RETURN
      END IF
      ILL=0
***
CF    MOJI(132)*1 HAIRETSU NO SYOKI SETTEI
***
      DO 50 I=1,132
        MOJI(I)=' '
   50 CONTINUE
***
CF    DAI 1 GYOU ME NO WAKU NO INSATSU
***
      DO 30 I=1,I1+2
      MOJI(I)='-'
   30 CONTINUE
      WRITE(6,100) ( MOJI(I),I=1,I1+2)
***
CF    DAI 2 GYOU ME KARA DAI (J+1) GYOUME NO NO INSATSU
***
      DO 20 J=1,J1
      MOJI(1)='|'
      DO 10 I=1,I1
        IF(IA(I,J).EQ.1) THEN
          MOJI(I+1)='*'
          ELSE IF(IA(I,J).EQ.0) THEN
          MOJI(I+1)=' '
          ELSE
          MOJI(I+1)='X'
          ILL=2000
        END IF
   10 CONTINUE
      MOJI(I1+2)='|'
      WRITE(6,100) ( MOJI(I),I=1,I1+2)
   20 CONTINUE
***
CF    DAI (J+2) GYOU ME NO WAKU NO INSATSU
***
      DO 40 I=1,I1+2
      MOJI(I)='-'
   40 CONTINUE
      WRITE(6,100) ( MOJI(I),I=1,I1+2)
  100 FORMAT(1H ,132A1)
      RETURN
      END
